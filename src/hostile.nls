hostiles-own[
  velocity
  rand-path
  cooldown-status
  finished?
  ]

to setup-hostiles
  if nb-cars-hostile > 0 [
    ; get the size of the base to deploy the car accordingly
    let base-min-pxcor min [pxcor] of (patches with [objectif? and pzcor = mapAlt])
    let base-max-pxcor max [pxcor] of (patches with [objectif? and pzcor = mapAlt])
    let base-min-pycor min [pycor] of (patches with [objectif? and pzcor = mapAlt])
    let base-max-pycor max [pycor] of (patches with [objectif? and pzcor = mapAlt])

    ; creation des voitures du convoi et cortege
    create-hostiles nb-cars-hostile
    ask hostiles
    [
      ; Init apparence NetLogo
      ;set shape "car" ;; CHANGER LA SHAPE DES HOSTILE
      ;set color red
      set shape "person soldier" ;; CHANGER LA SHAPE DES HOSTILE


      ; Init vars convois
      set speed 0.05 * simu-speed
      set maxdir 10 * simu-speed
      set state 2
      set heading random 360
      set roll 0
      set pitch 0
      set rand-path []
      set finished? true
      set dead? false
      set cooldown-status 0
      

      ; Visu
      set label who ; display the car names
    ]

    ; deploying the other car
    
    if nb-cars > 1 [
      ; ask non leader cars
      ask turtle-set hostiles
      [

        ; deploying
        set xcor base-min-pxcor + ((random 10) - 5)
        set ycor base-max-pycor + ((random 10) - 5)
        set zcor solAlt
      ]

    ]
  ]
end


to hostiles-think 
  ;ask hostiles [
  ;  let start-patch min-one-of (patches with [pzcor = mapAlt and not obstacle?]) [distance ?] ; on s'assure de choisir comme patch de depart un patch libre sans obstacle, sinon quand on split un convoi il se peut qu'il soit sur un obstacle et qu'il ne puisse jamais generer de chemin
  ;  let new-path plan-astar ([patch-at 0 0 (pzcor * -1)] of start-patch) (one-of patches with [base?]) false
  ;]
 random-move-path
   
end

;Hostiles Behaviors
to hostile-random-move ;Très random, donne des déplacements peu naturels. Marche a peu près, parfois ils sont retard et ils roulent sur les obstacles
  ;Possibilité de tourner légerement pendant le déplacement
  ifelse (detect-obstacle 4 15) = false ;si pas d'obstacle devant
  [fd speed if random 1000 < 10 [set heading random 360]] ;on avance de la vitesse de simulation possibilité de tourner de temps en temps
  [set heading  random 360] ;si non, on tourne comme un gros débile jusqu'à pouvoir avancer
end

to random-move-path ;Ici, l'idée c'est de générer un chemin vers un patch aléatoire et de s'y rendre
  ;let tmp-plan plan-astar patch-here one-of patches with [pzcor = 0 and not obstacle?] false

  let tmp-visu astar-visu
  set astar-visu false
  if nb-cars-hostile > 0 [

    ; Calcul du plan AStar pour chaque hostile ayant atteint son arrivée
    foreach sort-on [who] turtle-set hostiles with [finished? and not dead?] [
      let id ([who] of ?) ;id pour gérer avec astar observer hors turtle context
      if [finished?] of (turtle id) [;si pas de chemin on génère
        let tmp-patch patch ([pxcor] of (turtle id)) ([pycor] of (turtle id))  0
        let tmpplan plan-astar ( tmp-patch ) (one-of patches with [not obstacle? and pzcor = mapAlt]) (false)
        ask turtle id [set rand-path tmpplan if not empty? rand-path [set finished? false]]
      ]
    ]

      ;Déplacements selon les random-paths
      ask hostiles with [not finished? and not dead?] [ ; Tant qu'on n'a pas atteint le but

      ; Recupere le plan AStar
      if not empty? rand-path [
        ; Deplacement par waypoints: on se deplace jusqu'au prochain patch du chemin jusqu'à l'atteindre
        let next-patch first rand-path
        let zz pzcor
        set next-patch [patch-at 0 0 (zz - pzcor)] of next-patch ; mise a niveau de pzcor au cas ou le chemin a ete calculé sur un autre plan
        ; Deplacement vers le prochain waypoint
        if next-patch != patch-here [move-hostile next-patch false false]
        ; Si on a atteint ce patch, on le supprime de la liste, et on va donc continuer vers le prochain patch du chemin
        if patch-here = next-patch [
          set rand-path remove-item 0 rand-path
        ]
      ]

      ; Critere d'arret: on est a cote de l'objectif
      if empty? rand-path[
        set finished? true
      ]
    ]


  ]

  set astar-visu tmp-visu
end


to move-hostile [goal slowdown? cortege?]
  ;show (word "ici:" patch-here " goal:" goal)

  ; Calcule de l'angle avec la cible
  let headingFlag heading
  ifelse cortege?
  [ set headingFlag (towards goal) ] ; Si c'est un cortege, on veut qu'il suive toujours le leader par le chemin le plus court (surtout en play-mode ou le joueur n'est pas limite par le nowrap)
  [ set headingFlag (towards-nowrap goal) ]
  let dirCorrection subtract-headings headingFlag heading
  ; Arrondissement de l'angle (on ne veut pas faire de micro tournant)
  set dirCorrection precision dirCorrection 2
  ; Limite de l'angle, pour que ce soit plus realiste (la voiture ne peut pas faire un demi-tour sur place!)
  ifelse dirCorrection > maxdir [ ; limite a droite
    set dirCorrection maxdir
  ]
  [
    if dirCorrection < maxdir * -1 [ ; limite a gauche
      set dirCorrection maxdir * -1
    ]
  ]

  ; On tourne
  rt dirCorrection

  ; Limite de vitesse pour les voitures-cortege (pour pas qu'elles ne rentrent dans la voiture leader)
  let tmp-speed speed
  if slowdown? [
    if distance-nowrap goal < 1.1 [
      set tmp-speed tmp-speed / 20
    ]
    if distance-nowrap goal < 0.9 [
      set tmp-speed 0
    ]
  ]

  ; Deplacement!
  set pitch 0 ; make sure there's no pitch ever, else the car will disappear in the ground
  fd tmp-speed ; Avance
end



to hostile-fire
 ask hostiles [
   set cooldown-status cooldown-status  - 1 ;; On diminue le cooldown pour pouvoir tirer 
   ifelse count convois in-cone 10 60 > 0 [
     add-convoi-position xcor ycor zcor  ;; on enregistre la position du convoi
                                         ;; Pour l'instant la position du convoi n'est pas utilisée
     if cooldown-status < 1[  ;; S'il y a un convoi dans le champ de vision ET qu'on peut tirer
       set cooldown-status cooldown ;; On met le cooldown au max
                                    ;; TODO : Changer l'angle de l'hostile vers la cible 
       shoot ;; On tire :-)
     ]
   ]
   [ ;; si je vois personne dans cette zone je la retire des positions connu du convoi
     let tmp-list (list int (xcor / 10) int (ycor / 10) int (zcor / 10))
     if member? tmp-list convoi-position [
       set convoi-position remove tmp-list convoi-position
     ]
   ]
 ]
end
 
 
to add-convoi-position [xcord ycord zcord]
  let tmp-list (list int (xcor / 10) int (ycor / 10) int (zcor / 10))
  if not member? tmp-list convoi-position [
       set convoi-position lput tmp-list convoi-position
  ]
end